buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("com.bmuschko:gradle-nexus-plugin:2.3.1")
    }
}

plugins {
    id 'java'
    id("maven")
    id("signing")
}
/**
 * Set the java version. Version 8 and upwards should worked. Has been tested with Java 8 and 11
 */
def javaVersion = hasProperty('jdk') ? JavaVersion.toVersion(getProperty('jdk') as String) : JavaVersion.VERSION_11
apply plugin: "com.bmuschko.nexus"
java {
    modularity.inferModulePath = javaVersion >= JavaVersion.VERSION_1_9
}
compileJava {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (javaVersion < JavaVersion.VERSION_1_9) {
        excludes += ['**/module-info.java']
    }
}

test {
    useJUnitPlatform()
}
repositories {
    mavenCentral()
}

version "0.9.61"
group "net.mahdilamb"
ext.moduleName = 'net.mahdilamb.colormap'

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}
task swingDemo(type: JavaExec, dependsOn: ["testClasses"]) {
    classpath = sourceSets["test"].runtimeClasspath
    main = "net.mahdilamb.colormap.tests.InteractiveTest"
}
/**
 * Uncomment and run to insert colormap swatches into README.md
 */

task generateReadme(type: JavaExec, dependsOn: ["testClasses"]) {
    classpath = sourceSets["test"].runtimeClasspath
    main = "net.mahdilamb.colormap.reflect.GenerateReadme"
}
/**
 * Uncomment and run to insert colors
 */

task insertColors(type: JavaExec, dependsOn: ["testClasses"]) {
    classpath = sourceSets["test"].runtimeClasspath
    main = "net.mahdilamb.colormap.reflect.InsertColors"
}
/**
 * Uncomment and run to insert annotated colormaps into net.mahdilamb.colormap.Colormap
 */
/*
task insertColormaps(type: JavaExec, dependsOn: ["testClasses"]) {
    classpath = sourceSets["test"].runtimeClasspath
    main = "net.mahdilamb.colormap.reflect.InsertColormaps"
}*/

modifyPom {

    project {
        name = "Colormap"
        description = "A Java-based colormap generator."
        url = "https://github.com/mahdilamb/colormap"
        inceptionYear = "2020"

        licenses {
            license {
                name = "The Apache Software License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "repo"
            }
        }
        developers {
            developer {
                id = "mahdilamb"
                name = "Mahdi Lamb"
                email = "mahdilamb[at]gmail[dot]com"
            }
        }
        scm {
            url = "https://github.com/mahdilamb/colormap"
            connection = "scm:https://mahdilamb@github.com/mahdilamb/colormap.git"
            developerConnection = "scm:git://github.com/mahdilamb/colormap.git"
        }


    }

}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = !version.endsWith("SNAPSHOT")
    repositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    snapshotRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
}
